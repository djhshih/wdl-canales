task bam_to_fastq_paired {
	File input_bam
	String sample_id

	# Picard RevertSam uses a lot of temporary disk space!
	# (This is probably due to external sorting.)
	Int diskspace_gb = ceil(10 * size(input_bam, "GB"))
	Int memory_gb
	Int preemptible_tries

	command <<<
		set -euo pipefail

		# discard alignment information and sort reads by name
		# s.t. downstream insert size calculation is not biased
		java -Xmx${memory_gb}G \
			-Dsamjdk.compression_level=1 \
			-jar /usr/gitc/picard.jar RevertSam \
			INPUT=${input_bam} \
			OUTPUT=unaligned.bam \
			SANITIZE=true \
			ATTRIBUTE_TO_CLEAR=XT \
			ATTRIBUTE_TO_CLEAR=XN \
			ATTRIBUTE_TO_CLEAR=AS \
			ATTRIBUTE_TO_CLEAR=OC \
			ATTRIBUTE_TO_CLEAR=OP \
			SORT_ORDER=queryname \
			RESTORE_ORIGINAL_QUALITIES=true \
			REMOVE_DUPLICATE_INFORMATION=true \
			REMOVE_ALIGNMENT_INFORMATION=true \
			MAX_DISCARD_FRACTION=1.0
		rm ${input_bam}

		# NB no read group information is preserved, because external
		#    samples rarely have propr read group field values
		# outputs will be minimally compressed (level=1)
		java -Xmx${memory_gb}G \
			-XX:GCTimeLimit=50 -XX:GCHeapFreeLimit=10  \
			-Dsamjdk.buffer_size=131072 -Dsamjdk.use_async_io=true \
			-Dsamjdk.compression_level=1  \
			-jar /usr/gitc/picard.jar SamToFastq \
			TMP_DIR=. \
			INPUT=unaligned.bam \
			FASTQ=${sample_id}_R1.fastq.gz \
			SECOND_END_FASTQ=${sample_id}_R2.fastq.gz \
			UNPAIRED_FASTQ=${sample_id}_unpaired.fastq.gz \
			INCLUDE_NON_PF_READS=true
	>>>

	output {
		File fastq_r1 = "${sample_id}_R1.fastq.gz"
		File fastq_r2 = "${sample_id}_R2.fastq.gz"
		File fastq_unpaired = "${sample_id}_unpaired.fastq.gz"
	}

	runtime {
		docker: "broadinstitute/genomes-in-the-cloud:2.3.1-1500064817"
		memory: "${memory_gb} GB"
		cpu: "1"
		disks: "local-disk ${diskspace_gb} HDD"
		preemptible: preemptible_tries
	}
}
